apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.android.library'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'org.jetbrains.dokka' // Must be applied here for publish plugin.


kotlin {
  androidTarget()

  jvmToolchain(17)

  abiValidation {
    enabled = true
  }
}

tasks.named('check') { check ->
  check.dependsOn(
      // TODO: https://youtrack.jetbrains.com/issue/KT-78525
      tasks.named('checkLegacyAbi'),
  )
}

android {
  namespace 'timber.log'
  compileSdkVersion libs.versions.compileSdk.get().toInteger()

  defaultConfig {
    minSdkVersion libs.versions.minSdk.get()
    consumerProguardFiles 'consumer-proguard-rules.pro'
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
  }
}

dependencies {
  implementation libs.annotations

  testImplementation libs.annotations
  testImplementation libs.junit
  testImplementation libs.assertk
  testImplementation libs.robolectric

  lintPublish project(':timber-lint')
}
