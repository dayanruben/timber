import compat.patrouille.Severity

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.android.kotlin.multiplatform.library'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'org.jetbrains.dokka' // Must be applied here for publish plugin.
apply plugin: 'com.gradleup.compat.patrouille'

kotlin {
  androidLibrary {
    namespace = 'timber.log'
    compileSdk = libs.versions.compileSdk.get().toInteger()
    minSdk = libs.versions.minSdk.get().toInteger()

    optimization {
      // "it" --> https://issuetracker.google.com/issues/445115242
      it.consumerKeepRules.file('consumer-keep-rules.pro')
    }

    lint {
      it.textReport = true
    }
  }

  sourceSets {
    commonMain {
      dependencies {
        implementation libs.annotations
      }
    }
    commonTest {
      dependencies {
        implementation libs.annotations
        implementation libs.junit
        implementation libs.assertk
        implementation libs.robolectric
      }
    }
  }

  abiValidation {
    enabled = true
  }
}

compatPatrouille {
  java(8)
  kotlin('2.0.0')
  checkApiDependencies(Severity.ERROR)
  checkRuntimeDependencies(Severity.ERROR)
}

tasks.named('check') { check ->
  check.dependsOn(
      // TODO: https://youtrack.jetbrains.com/issue/KT-78525
      tasks.named('checkLegacyAbi'),
  )
}

dependencies {
  lintPublish project(':timber-lint')
}
